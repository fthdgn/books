apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "tr.name.fatihdogan.books"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 5
        versionName "0.2.3"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def supportVersion = '26.0.0-beta2'
    def playServiceVersion = '11.0.0'
    def archVersion = '1.0.0-alpha2'

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.google.android.gms:play-services-auth:$playServiceVersion"
    compile "com.google.firebase:firebase-crash:$playServiceVersion"
    compile "android.arch.persistence.room:runtime:$archVersion"
    compile "android.arch.lifecycle:runtime:$archVersion"
    compile "android.arch.lifecycle:extensions:$archVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.android.volley:volley:1.0.0'
    compile 'tr.name.fatihdogan:google-books-api:1.0'
    compile('com.github.ittianyu:BottomNavigationViewEx:1.1.4') {
        exclude group: 'com.android.support'
    }

    annotationProcessor "android.arch.persistence.room:compiler:$archVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archVersion"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.3-alpha', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

/**
 * Signing files.
 * For file format look at signingExample.gradle
 *
 * Google Books API is core part of this app.
 * You have to sign this application for both debug and release build.
 * Create a project from Google Cloud Platform
 * and enter your signing-certificate fingerprint
 * https://console.cloud.google.com/apis/credentials
 * https://support.google.com/cloud/answer/6158849?hl=en#installedapplications&android
 */
if (file('signingDebug.gradle').exists()) {
    apply from: 'signingDebug.gradle'
}
if (file('signingRelease.gradle').exists()) {
    apply from: 'signingRelease.gradle'
}

/**
 * These are optional gradle configurations for release builds,
 * such as Firebase and GitHub integration.
 */
if (file('release.gradle').exists())
    apply from: 'release.gradle'
