apply plugin: 'com.android.application'

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "tr.name.fatihdogan.books"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 4
        versionName "0.2.2"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'tr.name.fatihdogan:google-books-api:1.0'
    compile 'com.google.android.gms:play-services-auth:10.2.0'
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.firebase:firebase-crash:10.2.0'
    compile('com.github.ittianyu:BottomNavigationViewEx:1.1.4') {
        exclude group: 'com.android.support'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

/**
 * Signing files.
 * For file format look at signingExample.gradle
 *
 * Google Books API is core part of this app.
 * You have to sign this application for both debug and release build.
 * Create a project from Google Cloud Platform
 * and enter your signing-certificate fingerprint
 * https://console.cloud.google.com/apis/credentials
 * https://support.google.com/cloud/answer/6158849?hl=en#installedapplications&android
 */
if (file('signingDebug.gradle').exists()) {
    apply from: 'signingDebug.gradle'
}
if (file('signingRelease.gradle').exists()) {
    apply from: 'signingRelease.gradle'
}

/**
 * These are optional gradle configurations for release builds,
 * such as Firebase and GitHub integration.
 */
if (file('release.gradle').exists())
    apply from: 'release.gradle'
